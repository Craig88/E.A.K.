set view.characters.hobo.image = 'buttstacks'
set view.characters.hobo.name = 'Boris J. Buttstacks'

Hobo: Allo there... where d'ya think you're goin'? Just waltzin' into town? You ain't gettin' through unless I say so.

go menu

# menu
branch:
	- (if asked-onions) go menu-lets-go
	- (if asked-mission) go menu-URL
	- (if asked-who) go menu-mission
	- (default) go menu-who

# menu-who
Arca:
	- (unless asked-who) Who are you? -> who
	- (unless asked-passage) I don't have much time... can't you just let me pass? -> passage
	- (unless asked-kittens) I'm here to look for missing kittens... have you heard anything?  -> kittens
	- (unless asked-clothes) Why are you clothes so dirty? -> clothes

# menu-mission
Arca:
	- (unless asked-passage) I don't have much time... can't you just let me pass? -> passage
	- (unless asked-kittens) I'm here to look for missing kittens... have you heard anything? -> kittens
	- (unless asked-clothes) Why are you clothes so dirty? -> clothes
	- (unless asked-mission) Okay, what do I need to do to get into Ponyhead Bay? ->mission

# menu-URL
Arca:
	- (unless asked-kittens) I'm here to look for missing kittens... have you heard anything? -> kittens
	- (unless asked-clothes) Why are your clothes so dirty? -> clothes
	- (unless asked-onions) Okay, what's the first thing I need to get? -> onions

# menu-lets-go
Arca:
	- (unless asked-kittens) I'm here to look for missing kittens... have you heard anything? -> kittens
	- (unless asked-clothes) Why are your clothes so dirty? -> clothes
	- Fine. Let's go -> lets-go

# who
set asked-who
branch:
	- (if told-mayor) Hobo: The name's Boris J Buttstacks. As I've said, I'm the town's mayor. As in I rule th'whole town. As mayors do. And I'm the mayor, fo'real. I'm legit.
	- (default) Hobo: The name's Boris J Buttstacks, the town's mayor. As in I rule th'whole town. As mayors do. And I'm the mayor, fo'real. I'm legit.
		set told-mayor
go menu


# passage
set asked-passage
branch:
	- (if told-mayor) Hobo: Nah-ah. This is my town. As I might 'ave mentioned, I'm th'mayor. Completely legit. D'ya get it? I rule it here. And it ain't the first kid wantin' to visit that can get in just like that.
	- (default) Hobo: Nah-ah. This is my town. I'm th'mayor. Completely legit. D'ya get it? I rule it here. And it ain't the first kid wantin' to visit that can get in just like that.
		set told-mayor
go menu

# kittens
set asked-kittens
Hobo: Strange ya mentioned that. A funny lookin' chap with a tail came an' asked where he could find kittens, just t'other day. Didn't like the look of his jumper so I told 'im to bob off.

Arca:
	- Oh... did he say why?
		continue
	- What was his name?
		continue
	- Do you know where he was going?
		continue

Hobo: Nup, he didn't say.
go menu

# clothes
set asked-clothes
Hobo: What the Newt kinda question is that? No offense kid, but that's rich coming from someone dressed as a uncooked potato.
Arca:
	- It's a cape... capes are awesome!
		Hobo: Hmph! Who do you think you are? Wonder Walrus?

	- These are the only clothes I have.
		Hobo: Don't be lazy, there are lots of bins around 'ere. Tha's where I get all my clothes.

	- Potatoes are at the top of the root vegetable food chain.
	  Hobo: Hmmm. I've avoided potatoes ever since the accident...
		set game.characters.buttstacks.scared-of-potatoes

go menu

# mission
set asked-mission
Hobo: Well... Ya gonna need to go and fetch some stuff for me. Imma give you some addresses that will help ya find my stuff. Got it?

go menu

# onions
set asked-onions
Hobo: Well see, I'm a big fan o' pickled onions. Could eat a thousand an' beg fo'more. Ya gonna need to go to Bulbous Island and get me a big ol' jar of pickled onions, if ya please. I've written down the address, and ya gonna have to follow it. I'll help you out with this one, but after that you're on your own!

go menu

# lets-go
continue
